// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/03/a/PC.hdl

/**
 * A 16-bit counter with load and reset control bits.
 * if      (reset[t] == 1) out[t+1] = 0
 * else if (load[t] == 1)  out[t+1] = in[t]
 * else if (inc[t] == 1)   out[t+1] = out[t] + 1  (integer addition)
 * else                    out[t+1] = out[t]
 */

CHIP PC {
    IN in[16],load,inc,reset;
    OUT out[16];

    PARTS:
    //increment code
    Inc16(in=y,out=incresult);
    Mux16(a=y,b=incresult,sel=inc, out=o1);
    //load code
    Mux16(a=o1,b=in,sel=load,out=o2);
    //reset code
    Mux16(a=o2,b=false,sel=reset,out=o3);
        // ors to check if the loading bit is on or not - it shouldn't actually matter, but putting it in because it's nice!
    Or(a=load,b=inc,out=or1);
    Or(a=or1,b=reset,out=orfinal);
    // feed into register
    Register(in=o3,load=orfinal, out=out, out=y);
}
